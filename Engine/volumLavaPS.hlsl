cbuffer VariableBuffer : register(b0)
{
	float elapsed;
	float3 padding;
};


cbuffer ViewDirBuffer : register(b1)
{
	float4x4 rotationMatrix;
	float4 eyePos;
};



struct PixelInputType
{
	float4 position : SV_POSITION;
	float2 tex : TEXCOORD0;
	float3 normal : NORMAL;
	float4 msPos : MSPOS;
	float4 wPos : WPOS;
};

Texture2D shaderTexture;
SamplerState SampleType;


//
// Description : Array and textureless GLSL 2D/3D/4D simplex
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//               https://github.com/stegu/webgl-noise
//

float3 mod289(float3 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

float4 mod289(float4 x) {
	return x - floor(x * (1.0 / 289.0)) * 289.0;
}

float4 permute(float4 x) {
	return mod289(((x*34.0) + 1.0)*x);
}

float4 taylorInvSqrt(float4 r)
{
	return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(float3 v)
{
	const float2  C = float2(1.0 / 6.0, 1.0 / 3.0);
	const float4  D = float4(0.0, 0.5, 1.0, 2.0);

	// First corner
	float3 i = floor(v + dot(v, C.yyy));
	float3 x0 = v - i + dot(i, C.xxx);

	// Other corners
	float3 g = step(x0.yzx, x0.xyz);
	float3 l = 1.0 - g;
	float3 i1 = min(g.xyz, l.zxy);
	float3 i2 = max(g.xyz, l.zxy);

	//   x0 = x0 - 0.0 + 0.0 * C.xxx;
	//   x1 = x0 - i1  + 1.0 * C.xxx;
	//   x2 = x0 - i2  + 2.0 * C.xxx;
	//   x3 = x0 - 1.0 + 3.0 * C.xxx;
	float3 x1 = x0 - i1 + C.xxx;
	float3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
	float3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

	 // Permutations
	i = mod289(i);
	float4 p = permute(permute(permute(i.z + float4(0.0, i1.z, i2.z, 1.0)) + i.y + float4(0.0, i1.y, i2.y, 1.0)) + i.x + float4(0.0, i1.x, i2.x, 1.0));

	// Gradients: 7x7 points over a square, mapped onto an octahedron.
	// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
	float n_ = 0.142857142857; // 1.0/7.0
	float3  ns = n_ * D.wyz - D.xzx;

	float4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

	float4 x_ = floor(j * ns.z);
	float4 y_ = floor(j - 7.0 * x_);    // mod(j,N)

	float4 x = x_ * ns.x + ns.yyyy;
	float4 y = y_ * ns.x + ns.yyyy;
	float4 h = 1.0 - abs(x) - abs(y);

	float4 b0 = float4(x.xy, y.xy);
	float4 b1 = float4(x.zw, y.zw);

	//float4 s0 = float4(lessThan(b0,0.0))*2.0 - 1.0;
	//float4 s1 = float4(lessThan(b1,0.0))*2.0 - 1.0;
	float4 s0 = floor(b0)*2.0 + 1.0;
	float4 s1 = floor(b1)*2.0 + 1.0;
	float4 sh = -step(h, float4(0., 0., 0., 0.));

	float4 a0 = b0.xzyw + s0.xzyw*sh.xxyy;
	float4 a1 = b1.xzyw + s1.xzyw*sh.zzww;

	float3 p0 = float3(a0.xy, h.x);
	float3 p1 = float3(a0.zw, h.y);
	float3 p2 = float3(a1.xy, h.z);
	float3 p3 = float3(a1.zw, h.w);

	//Normalise gradients
	float4 norm = taylorInvSqrt(float4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
	p0 *= norm.x;
	p1 *= norm.y;
	p2 *= norm.z;
	p3 *= norm.w;

	// Mix final noise value
	float4 m = max(0.6 - float4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
	m = m * m;
	return 42.0 * dot(m*m, float4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}


float2x2 otherMat = float2x2(0.80, 0.60, -0.60, 0.80);

float2x2 makeRotMat(in float theta)
{
	float c = cos(theta);
	float s = sin(theta);
	return float2x2(c, -s, s, c);
}

float grid(float2 p)
{
	float s = sin(p.x)*cos(p.y);
	return s;
}


float flow(in float2 p)
{
	float z = 2.;
	float rz = 0.;
	float2 bp = p;
	for (float i = 1.; i < 7.; i++)
	{
		bp += elapsed * 1.5;
		float2 gr = float2(grid(p * 3. - elapsed * 2.), grid(p * 3. + 4. - elapsed * 2.)) * 0.4;
		gr = normalize(gr) * 0.4;
		gr = mul(gr, makeRotMat((p.x + p.y) *.3 + elapsed * 10.));
		p += gr * 0.5;

		rz += (sin(snoise(float3(p.x, 0, p.y) * 8.)) * 0.5 + 0.5) / z;

		p = lerp(bp, p, 0.5);
		z *= 1.7;
		p *= 2.5;
		p = mul(p, otherMat);
		bp *= 2.5;
		bp = mul(bp, otherMat);
	}
	return rz;
}


static const int NUM_OCTAVES = 10;
float turbulentFBM(float3 x)
{
	float sum = 0.0f;

	float frequency = 1.0f;
	float amplitude = 1.0f;

	float gain = .5317f;
	float lacunarity = 1.9357f;

	for (int i = 0; i < NUM_OCTAVES; ++i)
	{
		float r = snoise(frequency * x) * amplitude;
		r = r < 0 ? -r : r;
		sum += r;
		frequency *= lacunarity;
		amplitude *= gain;
	}

	return sum;
}


static const float LOWER = .4;
static const float UPPER = 1.2;


float4 LightPixelShader(PixelInputType input) : SV_TARGET
{
	input.normal = normalize(input.normal);

	float3 xyz = float3(input.wPos.x, input.wPos.y, input.wPos.z);

	xyz.xz -= 512;
	xyz.y -= elapsed * 5.f;		//move the texture upwards

	float3 scaledPos = xyz * .05f;

	float r = turbulentFBM(scaledPos);
	//float r = flow(scaledPos.xz);

	
	float g = smoothstep(LOWER * 2.f, UPPER, r);
	r = smoothstep(LOWER, UPPER, r);


	float4 colour = { r, g, 0., 1.f };

	//colour.rgb = pow(colour.xyz, float3(1.0f / 2.2f, 1.0f / 2.2f, 1.0f / 2.2f));

	return colour;
}



float3 blackbody(float t)
{
	t *= 2000.f;

	float u = (0.860117757 + 1.54118254e-4 * t + 1.28641212e-7 * t*t)
		/ (1.0 + 8.42420235e-4 * t + 7.08145163e-7 * t*t);

	float v = (0.317398726 + 4.22806245e-5 * t + 4.20481691e-8 * t*t)
		/ (1.0 - 2.89741816e-5 * t + 1.61456053e-7 * t*t);

	float x = 3. * u / (2.0*u - 8.0*v + 4.0);
	float y = 2. * v / (2.0*u - 8.0*v + 4.0);
	float z = 1. - x - y;

	float Y = 1.0;
	float X = Y / y * x;
	float Z = Y / y * z;

	float3x3 XYZtoRGB = float3x3
		(
			3.2404542, -1.5371385, -0.4985314,
			-0.9692660, 1.8760108, 0.0415560,
			0.0556434, -0.2040259, 1.0572252
			);

	return max(float3(0., 0., 0.), mul(float3(X, Y, Z), XYZtoRGB) * pow(t * 0.0004, 4.0));
}